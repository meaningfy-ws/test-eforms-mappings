#title: Test absence of runt entities aka empty individuals
#description: Checks for non-existence of runts or empty individuals/instances, i.e. RDF resources (entities) that have little to no information on them (no property asserted other than rdf:type)
#xpath: None
#was: empty_individuals (TED-929/TED-933) / empty_instances

PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX cccev: <http://data.europa.eu/m8g/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX epd: <http://data.europa.eu/a4g/resource/>
PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX fnml: <http://semweb.mmlab.be/ns/fnml#>
PREFIX fno: <https://w3id.org/function/ontology#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX idlab-fn: <http://example.com/idlab/function/>
PREFIX locn: <http://www.w3.org/ns/locn#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ql: <http://semweb.mmlab.be/ns/ql#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rml: <http://semweb.mmlab.be/ns/rml#>
PREFIX rr: <http://www.w3.org/ns/r2rml#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX tedm: <http://data.europa.eu/a4g/mapping/sf-rml/>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# A "runt" is a _minimal_ RDF subject resource that has little to no
# information associated with it other than a type assertion. It's like a
# short, insignificant word or line of text in typography. A traditional
# database analogue does not exist as schemas are typically rigid (a column is
# either required or not). In networking terms, it might be akin to a small,
# low-priority data packet that doesn't carry much substantive information.
# While runts may very well be a valid occurrence in RDF, they may -- more
# often than not -- signal data errors.
#
# Another situation could be that of a "widow" -- an _isolated_ RDF object
# resource that does not have statements of its own (not even a type
# assertion), similar to a typographic widow. It's like a word or line of text
# that stands alone at the top of a paragraph. In traditional databases this
# can rarely occur as referential integrity checks ensure referenced records
# exist. In networking terms, it's comparable to a data packet that has no
# preceding or subsequent packets in the flow. Widows are not a valid
# occurrence in RDF either, as objects with no information almost always signal
# a data error.
#
# In RML mapping, however, this situation cannot arise as referenced
# parentTriplesMaps of predicateObjectMaps (the only situation where a
# relationship is established) usually have a type associated, yielding at
# least a runt, but not a widow.

# This is a negated version of the SELECT counterpart

ASK {
    FILTER NOT EXISTS {
        ?s a ?what .
        FILTER (isIRI(?s))
        FILTER NOT EXISTS {
            ?s ?p ?o
            FILTER(?p not in (rdf:type))
        }
    }
}
